group 'holmes.ponderosa'
version '1.0-SNAPSHOT'

buildscript {
  ext.kotlin_version = '1.1.0-beta-38'
  ext.ktor_version = '0.2.4'

  repositories {
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    mavenCentral()
    jcenter()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'org.hidetake:gradle-ssh-plugin:2.8.0'
  }
}

apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'org.hidetake.ssh'

repositories {
  maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
  maven { url 'http://dl.bintray.com/kotlin/kotlinx.support' }
  mavenCentral()
}

remotes {
  web01 {
    host = '192.168.100.5'
    port = 22
    user = 'homeseer'

    // There's a bug in the ssh-plugin that doesn't allow us to auth.
    // So let's hack around this for a while.
    password = new File("$projectDir/unfortunately/password").text

    // being lazy and just symlinking it.
    agent = true
    identity = file('$projectDir/unfortunately/id_rsa')
  }
}

task npmstart(type: Exec) {
  workingDir "$projectDir/src/main/react"
  executable 'sh'
  args '-c', 'npm start'
}

task deploy() {
  doLast {
    ssh.run {
      session(remotes.web01) {
        put from: "$buildDir/libs/ponderosa-1.0-SNAPSHOT.war", into: '/opt/jetty9/webapps'
      }
    }
  }
}
deploy.dependsOn build


task buildReact(type: Exec) {
  inputs.file("src/main/react/package.json")
  inputs.dir("src/main/react/src")
  inputs.dir("src/main/react/public")
  outputs.dir("src/main/react/build")

  workingDir './src/main/react'
  executable 'sh'
  args '-c', 'npm run build'
}

war.dependsOn buildReact
war {
  from('src/main/react/build') {
    into 'WEB-INF/classes/public/react'
  }
}


dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

  compile "com.sparkjava:spark-core:2.5.5"
  compile "com.sparkjava:spark-template-handlebars:2.3"

  compile "org.slf4j:slf4j-simple:1.7.22"
  compile 'com.squareup.moshi:moshi:1.4.0'
  compile 'io.reactivex.rxjava2:rxjava:2.0.4'

  testCompile "com.google.truth:truth:0.31"
  testCompile "org.mockito:mockito-core:2.7.6"
  testCompile "com.nhaarman:mockito-kotlin:1.3.0"
}
