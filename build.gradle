group 'holmes.ponderosa'
version '1.0-SNAPSHOT'

buildscript {
  ext.kotlin_version = '1.1.2-2'

  repositories {
    maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'org.hidetake:gradle-ssh-plugin:2.8.0'
  }
}

apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'war'
apply plugin: 'org.hidetake.ssh'

repositories {
  maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.1' }
  maven { url 'http://dl.bintray.com/kotlin/kotlinx.support' }
  mavenCentral()
}

remotes {
  web01 {
    host = '192.168.1.5'
    port = 22
    user = 'homeseer'

    // There's a bug in the ssh-plugin that doesn't allow us to auth.
    // So let's hack around this for a while.
    password = new File("$projectDir/unfortunately/password").text

    // being lazy and just symlinking it.
    agent = true
    identity = file('$projectDir/unfortunately/id_rsa')
  }
}

task npmstart(type: Exec) {
  workingDir "$projectDir/src/main/react"
  executable 'sh'
  args '-c', 'npm start'
}

task deploy() {
  doLast {
    ssh.run {
      session(remotes.web01) {
        put from: "$buildDir/libs/ponderosa.war", into: '/opt/jetty9/webapps'
      }
    }
  }
}
deploy.dependsOn build


task buildReact(type: Exec) {
  inputs.file("src/main/react/package.json")
  inputs.dir("src/main/react/src")
  inputs.dir("src/main/react/public")
  outputs.dir("src/main/react/build")

  workingDir './src/main/react'
  executable 'sh'
  args '-c', 'npm run build'
}

war.dependsOn buildReact
war {
  archiveName = 'ponderosa.war'
  from('src/main/react/build') {
    into 'WEB-INF/classes/public/react'
  }
}

sourceSets {
  main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

  compile 'com.google.dagger:dagger:2.9'
  kapt 'com.google.dagger:dagger-compiler:2.9'
  compile "com.sparkjava:spark-core:2.5.5"
  compile "org.slf4j:slf4j-simple:1.7.22"

  compile 'com.thejholmes.russound:api:1.0.1'
  compile 'com.squareup.moshi:moshi:1.4.0'
  compile 'com.google.code.gson:gson:1.7.2'
  compile 'io.reactivex.rxjava2:rxjava:2.0.4'
  compile 'com.squareup.okio:okio:1.11.0'

  testCompile "junit:junit:4.12"
  testCompile "com.google.truth:truth:0.31"
  testCompile "org.mockito:mockito-core:2.7.21"
  testCompile "com.nhaarman:mockito-kotlin:1.4.0"
}
